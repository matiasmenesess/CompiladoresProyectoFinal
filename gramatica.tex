\documentclass{article}
\usepackage{amsmath}
\usepackage{syntax}

\begin{document}

\title{Gramática del Lenguaje C}
\maketitle

\setlength{\grammarindent}{6em}
\begin{grammar}

⟨Prog⟩ ::=
    [ ⟨DirPre⟩ ]
    ⟨FuncMain⟩
    { ⟨AltDefTop⟩ }*

⟨AltDefTop⟩ ::=
      ⟨DefFun⟩
    | ⟨Decl⟩

⟨DirPre⟩ ::=
    “\#include” ⟨Cabecera⟩

⟨Cabecera⟩ ::=
      “<” ⟨Id⟩ “.” ⟨Id⟩ “>”
    | “\”” ⟨Id⟩ “.” ⟨Id⟩ “\””

〈FuncMain〉 ::=
        "int" "main" "(" [ 〈Parms〉 ] ")" 〈Bloque〉


⟨DefFun⟩ ::=
    ⟨Specs⟩ ⟨Declr⟩ “(” [ ⟨Parms⟩ ] “)” ⟨Bloque⟩

⟨Parms⟩ ::=
    ⟨ListaParms⟩

⟨ListaParms⟩ ::=
    ⟨Parm⟩ { “,” ⟨Parm⟩ }

⟨Parm⟩ ::=
    ⟨Specs⟩ ⟨Id⟩


⟨Decl⟩ ::=
    ⟨Specs⟩ ⟨ListaInit⟩ “;”

⟨ListaInit⟩ ::=
    ⟨InitDecl⟩ { “,” ⟨InitDecl⟩ }

⟨InitDecl⟩ ::=
    ⟨Declr⟩ [ “=” ⟨Init⟩ ]

⟨Declr⟩ ::=
    { “*” [ ⟨Qual〉 ] } ⟨DirDeclr⟩

⟨DirDeclr⟩ ::=
      ⟨Id⟩
    | ⟨DirDeclr⟩ “(” [ ⟨ListaTipos⟩ ] “)”

⟨ListaTipos⟩ ::=
    ⟨Specs⟩ [ ⟨Declr⟩ ] { “,” ⟨Specs⟩ [ ⟨Declr⟩ ] }

〈Specs〉 ::= 〈QualTipo〉 | 〈BasTipo〉

〈QualTipo〉 ::= 〈Qual〉 〈BasTipo〉


⟨Qual⟩ ::=
    “const”

⟨BasTipo⟩ ::=
      “void”
    | “char”
    | “int”
    | “float”
    | “double”
    | ⟨DefStruct⟩

⟨DefStruct⟩ ::=
      “struct” [ ⟨Id⟩ ] “{” { ⟨Miembro⟩ } “}”
    | “struct” ⟨Id⟩

⟨Miembro⟩ ::=
      ⟨Acceso⟩
    | ⟨DeclStruct⟩

⟨Acceso⟩ ::=
      “public” “:”
    | “private” “:”

⟨DeclStruct⟩ ::=
    ⟨SpecQual⟩ ⟨ListaDeclStruct⟩ “;”

⟨SpecQual⟩ ::=
      ⟨BasTipo⟩
    | ⟨Qual⟩

⟨ListaDeclStruct⟩ ::=
    ⟨DeclrStruct⟩ { “,” ⟨DeclrStruct⟩ }

⟨DeclrStruct⟩ ::=
    ⟨Declr⟩ [ “:” ⟨ExprConst⟩ ]


⟨Stmt⟩ ::=
      ⟨StmtEtiq⟩
    | ⟨Bloque⟩
    | ⟨StmtExpr⟩
    | ⟨StmtSel⟩
    | ⟨StmtIter⟩
    | ⟨StmtSalto⟩

⟨Bloque⟩ ::=
    “{” { ⟨Decl⟩ } { ⟨Stmt⟩ } “}”

⟨StmtEtiq⟩ ::=
      ⟨Id⟩ “:” ⟨Stmt⟩
    | “case” ⟨ExprConst⟩ “:” ⟨Stmt⟩
    | “default” “:” ⟨Stmt⟩

⟨StmtExpr⟩ ::=
    [ ⟨Expr⟩ ] “;”

⟨StmtSel⟩ ::=
      “if” “(” ⟨Expr⟩ “)” ⟨Stmt⟩ [ “else” ⟨Stmt⟩ ]
    | “switch” “(” ⟨Expr⟩ “)” ⟨Stmt⟩

⟨StmtIter⟩ ::=
      “while” “(” ⟨Expr⟩ “)” ⟨Stmt⟩
    | “do” ⟨Stmt⟩ “while” “(” ⟨Expr⟩ “)” “;”
    | “for” “(” [ ⟨Expr⟩ ] “;” [ ⟨Expr⟩ ] “;” [ ⟨Expr⟩ ] “)” ⟨Stmt⟩

⟨StmtSalto⟩ ::=
    “continue” “;”
    | “break” “;”
    | “return” [ ⟨Expr⟩ ] “;”

⟨Expr⟩ ::=
    ⟨AsigExpr⟩ { “,” ⟨AsigExpr⟩ }

⟨AsigExpr⟩ ::=
      ⟨CondExpr⟩
    | ⟨UnExpr⟩ ⟨OpAsig⟩ ⟨AsigExpr⟩

⟨OpAsig⟩ ::=
    “=” | “*=“ | “/=“ | “%=“ | “+=“ | “-=“ | “<<=“ | “>>=“ | “&=“ | “^=“ | “|=“

⟨CondExpr⟩ ::=
    ⟨OrExpr⟩ [ “?” ⟨Expr⟩ “:” ⟨CondExpr⟩ ]

⟨OrExpr⟩ ::=
    ⟨AndExpr⟩ { “||” ⟨AndExpr⟩ }

⟨AndExpr⟩ ::=
    ⟨EqExpr⟩ { “&&” ⟨EqExpr⟩ }

⟨EqExpr⟩ ::=
    ⟨RelExpr⟩ { ( “==” | “!=” ) ⟨RelExpr⟩ }

⟨RelExpr⟩ ::=
    ⟨ShiftExpr⟩ { ( “<” | “>” | “<=” | “>=” ) ⟨ShiftExpr⟩ }

⟨ShiftExpr⟩ ::=
    ⟨AddExpr⟩ { ( “<<” | “>>” ) ⟨AddExpr⟩ }

⟨AddExpr⟩ ::=
    ⟨MulExpr⟩ { ( “+” | “-” ) ⟨MulExpr⟩ }

⟨MulExpr⟩ ::=
    ⟨CastExpr⟩ { ( “*” | “/” | “%” ) ⟨CastExpr⟩ }

⟨CastExpr⟩ ::=
      ⟨UnExpr⟩
    | “(” ⟨TypeName⟩ “)” ⟨CastExpr⟩

⟨UnExpr⟩ ::=
      ⟨PostExpr⟩
    | “++” ⟨UnExpr⟩
    | “--” ⟨UnExpr⟩
    | ⟨OpUn⟩ ⟨CastExpr⟩
    | “sizeof” ⟨UnExpr⟩
    | “sizeof” “(” ⟨TypeName⟩ “)”

⟨OpUn⟩ ::=
      “*” | “"&"” | “+” | “-” | “"~"” | “!”

⟨PostExpr⟩ ::=
      ⟨Primary⟩
    | ⟨PostExpr⟩ “(” [ ⟨ArgList⟩ ] “)”
    | ⟨PostExpr⟩ “.” ⟨Id⟩
    | ⟨PostExpr⟩ “->” ⟨Id⟩
    | ⟨PostExpr⟩ “++”
    | ⟨PostExpr⟩ “--”

⟨Primary⟩ ::=
      ⟨Id⟩
    | ⟨Const⟩
    | ⟨Str⟩
    | “(” ⟨Expr⟩ “)”

⟨ArgList⟩ ::=
    ⟨AsigExpr⟩ { “,” ⟨AsigExpr⟩ }

⟨Const⟩ ::=
      ⟨IntC⟩
    | ⟨CharC⟩
    | ⟨FloatC⟩

⟨TypeName⟩ ::=
    ⟨Specs⟩ [ ⟨AbsDeclr⟩ ]

⟨AbsDeclr⟩ ::=
    ⟨Ptr⟩ [ ⟨DirAbsDeclr⟩ ]

⟨DirAbsDeclr⟩ ::=
      “(” ⟨AbsDeclr⟩ “)”
    | “[” ⟨DirAbsDeclr⟩ “]” “(” [ ⟨ListaTipos⟩ ] “)”

⟨Ptr⟩ ::=
    { “*” [ ⟨Qual⟩ ] }

⟨Init⟩ ::=
    ⟨AsigExpr⟩

\end{grammar}

\end{document}
